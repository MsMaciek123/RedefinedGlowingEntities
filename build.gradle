plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.3'
}

group = 'me.msmaciek'
version = '1.0.3'

repositories {
    mavenCentral()
    maven { url 'https://repo.codemc.io/repository/maven-releases/' }
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url 'https://jitpack.io'
    }
    mavenLocal()
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.20.2-R0.1-SNAPSHOT"

    compileOnly 'com.github.retrooper:packetevents-spigot:2.5.0'
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    minimize()

    if (project.hasProperty('shadowJar_destinationDirectory')) {
        destinationDirectory.set(file(getProperty('shadowJar_destinationDirectory')))
    }

    archiveFileName = "${project.name}-${project.version}.jar"
    exclude "hotswap-agent.properties"
}
